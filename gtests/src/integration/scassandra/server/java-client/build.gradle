apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'signing'
apply plugin: 'maven'
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public"}
    }
}

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

group 'org.scassandra'
jar.baseName = 'java-client'

compileJava {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

if (!project.hasProperty("ossrhUsername")) {
    ext.ossrhUsername = "dummy"
}
if (!project.hasProperty("ossrhPassword")) {
    ext.ossrhPassword = "dummy"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url "https://oss.sonatype.org/content/groups/staging"
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    baseName = 'java-client'
    from sourceSets.main.allJava
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = 'java-client'
    from 'build/docs/javadoc'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourceJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.artifactId = 'java-client'

            pom.project {
                name 'Scassandra Java Client'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Allows Java applications to use Scassandra'
                url 'https://github.com/scassandra/scassandra-java-client'

                scm {
                    connection 'scm:git:git@github.com:scassandra/scassandra-java-client.git'
                    developerConnection 'git@github.com:scassandra/scassandra-java-client.git'
                    url 'https://github.com/scassandra/scassandra-java-client'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'chbatey'
                        name 'Christopher Batey'
                        email 'christopher.batey@gmail.com'
                    }
                    developer {
                        id 'apojha'
                        name 'Alexandra Ojha'
                        email 'alexojha@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile project(':cql-antlr')
    compile project(':server')
    compile "org.apache.httpcomponents:httpclient:$httpClientVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "junit:junit:$junitVersion"
    testCompile "ch.qos.logback:logback-classic:$logbackVersion"
    testCompile "org.mockito:mockito-all:$mockitoVersion"
    testCompile "com.github.tomakehurst:wiremock:1.46"
    testCompile "nl.jqno.equalsverifier:equalsverifier:1.7.3"
    testCompile "com.googlecode.jarjar:jarjar:1.3"
    testCompile "com.datastax.cassandra:cassandra-driver-core:$javaDriverVersion"
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

shadowJar {
    append('application.conf')
    relocate 'org.apache.cassandra', 'scassandra.org.apache.cassandra'
    relocate 'org.mortbay', 'scassandra.org.mortbay'
    relocate 'com.google.common', 'scassandra.com.google.common'
    relocate 'org.apache.http', 'scassandra.org.apache.http'
    relocate 'org.apache.commons', 'scassandra.org.apache.commons'
    relocate 'com.google.code.gson', 'scassandra.com.google.code.gson'
    relocate 'org.scassandra.server', 'scassandra.org.scassandra.server'

    classifier = 'standalone'
    baseName = 'java-client'


    doFirst {
        def jar = project.tasks.jar
        String fileName = jar.archiveName - ("." + jar.extension) + "-standalone." + jar.extension
        File jarFile = new File(jar.destinationDir, fileName)
        Date date = new Date()
        String name = jar.baseName
        project.artifacts.add('archives',
                [
                        getClassifier: { -> "standalone" },
                        getDate      : { -> date },
                        getExtension : { -> "jar" },
                        getType      : { -> "jar" },
                        getFile      : { -> jarFile },
                        getName      : { -> name }
                ] as PublishArtifact
        )

    }
}

install.dependsOn shadowJar
uploadArchives.dependsOn shadowJar




buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven { url "https://oss.sonatype.org/content/groups/public" }
    }
    dependencies {
        classpath "com.github.maiflai:gradle-scalatest:0.7"
    }
}

apply plugin: "com.github.maiflai.scalatest"
apply plugin: 'scala'
apply plugin: 'signing'
apply plugin: 'maven'

group 'org.scassandra'
jar.baseName = 'scassandra-codec_2.11'

if (!project.hasProperty("ossrhUsername")) {
    ext.ossrhUsername = "dummy"
}
if (!project.hasProperty("ossrhPassword")) {
    ext.ossrhPassword = "dummy"
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url "https://oss.sonatype.org/content/groups/staging"
    }
}

compileJava {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}


compileScala {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    baseName = 'scassandra-codec_2.11'
    from sourceSets.main.allScala
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = 'scassandra-codec'
    excludes = ['**/**']
    from 'build/docs/javadoc'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourceJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.artifactId = 'scassandra-codec_2.11'

            pom.project {
                name 'Scassandra codec'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Scassandra codec'
                url 'https://github.com/scassandra/'

                scm {
                    connection 'scm:git:git@github.com:scassandra/scassandra-server.git'
                    developerConnection 'git@github.com:scassandra/scassandra-server.git'
                    url 'https://github.com/scassandra/scassandra-server'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'chbatey'
                        name 'Christopher Batey'
                        email 'christopher.batey@gmail.com'
                    }
                    developer {
                        id 'tepafoo'
                        name 'Dogan Narinc'
                        email 'tepafoo@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile "org.scala-lang:scala-library:$scalaVersion"
    compile "org.scodec:scodec-core_2.11:$scodecVersion"
    compile "com.google.guava:guava:$guavaVersion"

    testCompile 'org.scalacheck:scalacheck_2.11:1.12.4'
    testCompile "org.scalatest:scalatest_2.11:2.2.3"

    testRuntime 'org.pegdown:pegdown:1.1.0'
}

test {
    maxParallelForks = 1
}
